# Maintainer: Blue DeviL // SCT <bluedevil.SCT@gmail.com>

_pkgname=binwalk
pkgname="${_pkgname}2"
pkgver=2.4.3
pkgrel=1
pkgdesc='Tool for searching a given binary image for embedded files'
url='https://github.com/OSPG/binwalk'
arch=(any)
license=(MIT)
depends=(
  python
  python-setuptools
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
)
optdepends=(
  'python-opengl: binviz module support'
  'python-capstone: disassembly support'
  'python-matplotlib: entropy plotting'
  'python-pyqtgraph: entropy plotting'
  'python-numpy: entropy module'
  'python-numba: entropy module'
  'python-gnupg: pgp plugin'
  'arj: ARJ decompression support'
  'cabextract: CAB archive support'
  'cpio: CPIO archive support'
  'gzip: GZIP decompression support'
  'mtd-utils: JFFS filesystem support'
  'p7zip: ZIP, LZMA and ISO decompression support'
  'squashfs-tools: squashfs support'
  'tar: TAR archive support'
  'bzip2: BZIP2 archive support'
  'unrar: RAR decompression support'
  'xz: XZ decompression support'
  'lhasa: LHA support'
  'sleuthkit: forensic analysis support'
)
source=(
  "${url}/archive/v${pkgver}/${_pkgname}-${pkgver}.tar.gz"
)
sha512sums=('521c6af602ceda22ed55cb532f1831813953a517c5059bbd6defe77a10a2ef44b22916f73f25212ef2c6cb7a3b8bb3ff9357eb4ef45dc766290e9946004b8698')

build() {
  cd ${_pkgname}-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  cd ${_pkgname}-${pkgver}
  python -m venv --clear --without-pip --system-site-packages .testenv
  .testenv/bin/python -m installer dist/*.whl
  .testenv/bin/python -m pytest
}

package() {
  cd ${_pkgname}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  # prevent name conflicts
  mv "${pkgdir}/usr/bin/binwalk" "${pkgdir}/usr/bin/binwalk2"
  install -Dm 644 ./*.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
